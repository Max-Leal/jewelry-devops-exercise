terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.0"

    }
  }

  backend "s3" {
    bucket         = "max-tfstate-bucket-jewelry"
    key            = "tfstate/terraform.tfstate"
    region         = "us-east-1"
    dynamodb_table = "terraform-state-lock-max-jewelry"
    encrypt        = true
  }
}

provider "aws" {
  region = "us-east-1"
}

resource "aws_security_group" "sgJewelry" {
  name        = var.security_group_name
  description = "Security group for the Jewelry"
  vpc_id      = var.vpc_id

  dynamic "ingress" {
    for_each = [22, 5173, 8080]

    content {
      description = "Allow port ${ingress.value}"
      from_port   = ingress.value
      to_port     = ingress.value
      protocol    = "tcp"
      cidr_blocks = ["0.0.0.0/0"]
    }
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = var.security_group_name
  }
}

resource "aws_subnet" "jewelry_subnet" {
  vpc_id            = var.vpc_id
  cidr_block        = "172.30.133.0/24"
  availability_zone = "us-east-1a"

  tags = {
    Name = "jewelry-subnet-max"
  }
}

resource "aws_instance" "instance-jewelry" {

  ami                         = var.ami_id
  instance_type               = var.instance_type
  subnet_id                   = aws_subnet.jewelry_subnet.id
  vpc_security_group_ids      = [aws_security_group.sgJewelry.id]
  associate_public_ip_address = true
  user_data = base64encode(<<-EOF
    #!/bin/bash
    apt-get update
    apt install -y docker.io git build-essential
    systemctl start docker
    systemctl enable docker
    docker container stop jewelry-app 2> /dev/null
    cd /root
    rm -rf jewelry-devops-exercise/
    git clone https://github.com/Max-Leal/jewelry-devops-exercise.git
    cd ./jewelry-devops-exercise/modulo7-iac_tooling
    
    make docker-run      
  EOF
  )

  tags = {
    Name = "ec2-jewelry-max"
  }
}

output "instance_public_ip" {
  description = "Ip of the instance running the jewelry"
  value       = aws_instance.instance-jewelry.public_ip
}

output "app_url" {
  description = "Ip to access the app"
  value       = "APP: http://${aws_instance.instance-jewelry.public_ip}:8080"
}

variable "aws_region" {
  type    = string
  default = "us-east-1"
}

variable "security_group_name" {
  type    = string
  default = "jewelry-max"
}

variable "vpc_id" {
  type    = string
  default = "vpc-06786ee7f7a163059"
}

variable "instance_type" {
  type    = string
  default = "t3.micro"
}

variable "ami_id" {
  type    = string
  default = "ami-0ecb62995f68bb549"
}